buildscript {
    ext {
        grpc_version = "1.31.1"
        grpc_spring_boot_version = "3.5.7"
        protobuf_version = "3.13.0"
    }
}

plugins {
    id 'idea'
    id 'java'
    id 'signing'
    id 'io.freefair.lombok' version '5.1.1'
    id "com.google.protobuf" version "0.8.13"
}
allprojects {
    group = 'cz.iamceph.grpc-helper'
    version = '1.0.0-SNAPSHOT'
    sourceCompatibility = JavaVersion.VERSION_11
    generateLombokConfig.enabled = false

    repositories {
        jcenter()
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'signing'
    apply plugin: 'io.freefair.lombok'
    apply plugin: 'maven-publish'

    dependencies {
        compileOnly "org.apache.tomcat:annotations-api:6.0.53"
        compileOnly group: 'org.jetbrains', name: 'annotations', version: '20.0.0'
    }


    test {
        useJUnitPlatform()
    }

    publishing {
        publications {
            //Publishing of JARs into the Nexus
            maven(MavenPublication) {
                versionMapping {
                    usage('java-api') {
                        fromResolutionOf('runtimeClasspath')
                    }
                    usage('java-runtime') {
                        fromResolutionResult()
                    }
                }
                from components.java
            }
        }

        //Respositories settings
        repositories {
            maven {
                credentials {
                    username System.getenv("MAVEN_REPO_USER")
                    password System.getenv("MAVEN_REPO_PASS")
                }

                name = 'TSG-REPO'
                url = project.version.endsWith('-SNAPSHOT')
                        ? "https://nexus3.monetplus.cz/repository/tsg-snapshots/"
                        : "https://nexus3.monetplus.cz/repository/tsg-releases/"
            }
        }
    }
}